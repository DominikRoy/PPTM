(*Dolev-Yao model Open Channel*)
(*Channel between UAV A and UAV B*)
free c:channel.


(* RSA modulus *)
type N.

(* The attacker should not have knowledge of the actual identifier of the capsule mapped from  f (see paper Sec.4.2 step (4)) (secret).
Although, we define the secret as "weak" to check if offline brute-force or dictionary attacks are feasible. *)

free dA1: bitstring [private].
weaksecret dA1.
free dB1k: bitstring [private].
weaksecret dB1k.


(* Types, Constants and Variables *)
type radius.
type length.
type angle.
type origin.
type nonce.
type vector.
type index.




(* A formal query, specifying the attacker can't ever be leaked the
actual capsule identifiers during the protocol. *)
query attacker(dA1).
query attacker(dB1k).

(*Verify the non interference property for the capsule identifiers A and B, which means that 
the different secrets are indistingishuable for the adversary strong secrecy.*)
noninterf dB1k.
noninterf dA1.

(*Auxiliary Functions*)
fun hash(bitstring):bitstring.
fun map(bitstring):bitstring.(*secure function of 2H(x)+1*)
fun append(bitstring,bitstring):bitstring.
fun mod(bitstring,N):bitstring.
fun exp_mod(bitstring,bitstring,N):bitstring.
fun inv_mod(bitstring,N):bitstring.
fun extract_elem(vector,index):bitstring.
fun add_elem(vector,bitstring,index):vector.
fun map_trajectory(origin,angle,radius,length):vector.
(* Type converter *)
fun nonce_to_bitstring(nonce): bitstring [data,typeConverter].
(*Events*)
event end_UAV_A(vector).
event end_UAV_B(bitstring).



(* The process for UAV A*)
let uavA(nB:N) =
    (* Step (3) of Sec.4.2 in our paper*)
    new O1:origin;
    new T1:angle;
    new r1:radius;
    new h1:length;
    new xi1:nonce;

    (* Step (6) of Sec.4.2, Eq.9*)
    let cA1 = exp_mod(nonce_to_bitstring(xi1), dA1, nB) in
    (* Step (7) of Sec.4.2*)
    out (c,(cA1,O1,T1,r1,h1));
    (* Step (11) of Sec.4.2*)
    in (c, cB1:vector);
    new k:index;
    let cB1k = extract_elem(cB1,k) in
    (* Step (12) of Sec.4.2, Eq. 12*)
    let wA1k = hash(mod(nonce_to_bitstring(xi1),nB)) in 
    (* Step (15) of Sec.4.2*)
    if wA1k = cB1k then ( 
        (* Step (13) of Sec.4.2, Eq. 14*)
        let wA1 = hash(mod(append(nonce_to_bitstring(xi1),dA1),nB)) in
        (* Step (16) of Sec.4.2 ==> paper*)
        out(c,wA1);
    
        event end_UAV_A(cB1)).








(* The process for UAV B*)
let uavB(nB:N) =
    (* Step (8) of Sec.4.2*)
    in(c, (cA1:bitstring,O1:origin,T1:angle,r1:radius,h1:length));
    let GB1 = map_trajectory(O1,T1,r1,h1) in
    new k:index;
    (* Step (9) of Sec.4.2*)
    let eB1k = inv_mod(dB1k,nB) in
    (* Step (10) of Sec.4.2, Eq. 10*)
    let x = exp_mod(cA1,eB1k,nB) in
    (* Step (10) of Sec.4.2, Eq. 11*)
    let cB1k = hash(x) in
    new cB1:vector;
    let cB1 = add_elem(cB1,cB1k,k) in
    (* Step (11) of Sec.4.2*)
    out (c, cB1);
    (* Step (16) of Sec.4.2*)
    in (c, wA1:bitstring);
    (* Step (17) of Sec.4.2, Eq. 15*)
    if wA1 = hash(mod(append(x,dB1k),nB)) then (
        event end_UAV_B(wA1)).

process
    new nB: N;
    ((!uavA(nB)) | (!uavB(nB)))